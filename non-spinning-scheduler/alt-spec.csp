include "channel-spec.csp"

-- GENETIC ALT SPECIFICATION

channel select : Processes
channel return : Channels

ALT_SPEC(selector, pairs, writing, waiting) =
    ([] (c, p) : pairs @ write.c.p?mess -> ALT_SPEC(selector, pairs, union(writing, {c}), waiting))
    []
    if waiting then
        ([] c : writing @ return.c -> ALT_SPEC(selector, pairs, diff(writing, {c}), false))
    else
        select.selector ->
        ALT_SPEC(selector, pairs, writing, true)

alphaALT_SPEC(altpid) = { write.c.p.v, return.c, select.altpid | c <- Channels, p <- Processes, v <- Values }

ALT_USER(pid) =
    select.pid ->           -- Kick off the alt
    return?idx ->           -- Get a returned index of a ready channel back
    start_read.idx.pid ->   -- Do the actual read from the chosen channel
    transmit.idx?mess ->
    read.idx.pid!mess ->    
    ALT_USER(pid)           -- Recurse

alphaALT_USER(pid) = 
{ 
    select.pid, 
    return.c, 
    start_read.c.pid, 
    transmit.c.v, 
    read.c.pid.v | c <- Channels, v <- Values 
}

PRIVATE_ALT_SPEC_SYSTEM(pid) = { transmit.c.v, return.c | c <- Channels, v <- Values }

LEFT_PROCS(pairs) =
    ||| (c, p) : pairs @ LEFT(p, c)

alphaLEFT_PROCS(<>, <>) = {}
alphaLEFT_PROCS(<p>^writing_procs, <c>^chans) = union(
        {
            write.c.p.v, 
            transmit.c.v, 
            ack.c.p
            | v <- Values
        },
        alphaLEFT_PROCS(writing_procs, chans)
    )

ALT_SPEC_SYSTEM(selector, chans, writing_procs) = (
(
    (
        (LEFT_PROCS(set(zip(chans, writing_procs))))
        [alphaLEFT_PROCS(writing_procs, chans) || alphaALT_SPEC(selector)]
        ALT_SPEC(selector, set(zip(chans, writing_procs)), {}, false)
    )
    [union(alphaLEFT_PROCS(writing_procs, chans), alphaALT_SPEC(selector)) || alphaALT_USER(selector)]
    ALT_USER(selector)
) \ union(PRIVATE_ALT_SPEC_SYSTEM(selector), Union({PRIVATE_CHANNEL_SPEC(c) | c <- Channels})))

-- END GENERIC ALT MODEL